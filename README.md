# krnlnex-fne
使用 C++ (VC6.0) 开发的易语言枚举文件子目录支持库（原“辅助调试支持库”）

从2.0#0版开始支持静态编译，并隐藏多个与易语言官方特殊功能支持库重复的命令（如“调试输出”“调用子程序”“取变量数据地址”等），只保留“枚举文件()”“枚举子目录()”两条命令，支持库也重命名为“枚举文件子目录支持库”，但仍保持向后兼容。

注：本支持库（原“辅助调试支持库”）中的许多函数已被改进并移入易语言官方的特殊功能支持库（spec.fne）中。此库源码已是旧版，且不再更新，仅供参考。但是此库中的“枚举文件”“枚举子目录”却是我经常用到的很实用的函数。

----

Liigo 20220725: 临时用到此功能却一时之间找不到编译后的krnlnex.fne及其静态库，包括网上各处也不易找到最新版本。要想自己编译，可配置编译环境也挺费时费力，况且我可不愿意再安装使用那破旧不堪的VC 6.0。

此处提供一份纯易语言代码实现的遍历文件和遍历子目录函数，以供不时之需：

```
.版本 2
.支持库 spec

.子程序 庄子_目录_遍历文件, , 公开, 递归遍历指定目录及其所有子目录内的所有文件。by Liigo, 20220725.
.参数 目录, 文本型, , 必须是全路径目录，是否已目录分隔符(\)结尾均可接受
.参数 文件处理子程序地址, 整数型, , 一个文本参数（接收全路径文件名），无返回值
.参数 递归, 逻辑型, 可空, 如果被省略，默认为真，即默认递归遍历。如果为假，不遍历子目录内文件。
.局部变量 file, 文本型
.局部变量 dir, 文本型

.如果真 (是否为空 (递归))
    递归 ＝ 真
.如果真结束
.如果真 (目录 ＝ “”)
    返回 ()
.如果真结束

.如果真 (取文本右边 (目录, 1) ＝ “\” 或 取文本右边 (目录, 1) ＝ “/”)
    目录 ＝ 取文本左边 (目录, 取文本长度 (目录) － 1)
.如果真结束

' 先找指定目录内文件
file ＝ 寻找文件 (目录 ＋ “\*”, )
.如果真 (file ≠ “”)
    .循环判断首 ()
        调用子程序 (文件处理子程序地址, , , 目录 ＋ “\” ＋ file)
        file ＝ 寻找文件 (, )
    .循环判断尾 (file ≠ “”)

.如果真结束

.如果真 (递归 ＝ 假)
    返回 ()
.如果真结束

' 递归子目录逐一遍历文件
dir ＝ 寻找文件 (目录 ＋ “\*”, #子目录)
.如果真 (dir ≠ “”)
    .循环判断首 ()
        .如果真 (dir ≠ “.” 且 dir ≠ “..”)
            庄子_目录_遍历文件 (目录 ＋ “\” ＋ dir, 文件处理子程序地址, 递归)
        .如果真结束

        dir ＝ 寻找文件 (, #子目录)
    .循环判断尾 (dir ≠ “”)
.如果真结束


.子程序 庄子_目录_遍历子目录, , 公开, 递归遍历指定目录内的所有子目录。by Liigo, 20220725.
.参数 目录, 文本型, , 必须是全路径目录，是否已目录分隔符(\)结尾均可接受
.参数 子程序地址, 整数型, , 一个文本参数（接收全路径子目录），无返回值
.参数 递归, 逻辑型, 可空, 如果被省略，默认为真，即默认递归遍历。如果为假，不遍历子目录内子目录。
.局部变量 file, 文本型
.局部变量 dir, 文本型
.局部变量 fulldir, 文本型

.如果真 (是否为空 (递归))
    递归 ＝ 真
.如果真结束
.如果真 (目录 ＝ “”)
    返回 ()
.如果真结束

.如果真 (取文本右边 (目录, 1) ＝ “\” 或 取文本右边 (目录, 1) ＝ “/”)
    目录 ＝ 取文本左边 (目录, 取文本长度 (目录) － 1)
.如果真结束

' 递归遍历目录内所有子目录
dir ＝ 寻找文件 (目录 ＋ “\*”, #子目录)
.如果真 (dir ≠ “”)
    .循环判断首 ()
        .如果真 (dir ≠ “.” 且 dir ≠ “..”)
            fulldir ＝ 目录 ＋ “\” ＋ dir
            调用子程序 (子程序地址, , , fulldir)
            .如果真 (递归)
                庄子_目录_遍历子目录 (fulldir, 子程序地址, 真)
            .如果真结束

        .如果真结束

        dir ＝ 寻找文件 (, #子目录)
    .循环判断尾 (dir ≠ “”)
.如果真结束

```
